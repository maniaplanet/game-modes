#RequireContext CGameCtnEditorPluginScriptHandler

Text CreateManialink()
{
	declare MLText =
	"""
		<script><!--
			Void UpdateValues() {
				declare CursorX for Page = 0;
				declare CursorY for Page = 0;
				declare CursorZ for Page = 0;
				declare CursorDirectionText for Page = "";
				declare LblCursorXValue <=> (Page.GetFirstChild("CursorXValue") as CMlLabel);
				declare LblCursorYValue <=> (Page.GetFirstChild("CursorYValue") as CMlLabel);
				declare LblCursorZValue <=> (Page.GetFirstChild("CursorZValue") as CMlLabel);
				declare LblCursorDirectionValue <=> (Page.GetFirstChild("CursorDirectionValue") as CMlLabel);
				LblCursorXValue.SetText("" ^ CursorX);
				LblCursorYValue.SetText("" ^ CursorY);
				LblCursorZValue.SetText("" ^ CursorZ);
				LblCursorDirectionValue.SetText(CursorDirectionText);
			}
			main () {
				declare BoxOffset = 0;
				while(True) {
					yield;
					UpdateValues();
				}
			}
		--></script>
		<frame posn="140 -12 0">
			<quad id="CursorCoordBox" posn="0 0" z-index="-1" sizen="20 28" style="Bgs1" substyle="BgList" ScriptEvents="1"/>
			<label id="CursorXLabel" text="Cursor" posn="2 -2"/>
			<label id="CursorXLabel" text="X" posn="2 -7"/>
				<label id="CursorXValue" text="XXX" posn="8 -7"/>
			<label id="CursorYLabel" text="Y" posn="2 -12"/>
				<label id="CursorYValue" text="XXX" posn="8 -12"/>
			<label id="CursorZLabel" text="Z" posn="2 -17"/>
				<label id="CursorZValue" text="XXX" posn="8 -17"/>
			<label id="CursorDirectionValue" text="XXX" posn="2 -22"/>
		</frame>
	""";
	return MLText;
}

/////////////////////////////////////
// Main
/////////////////////////////////////

main()
{
	log("CursorCoords.Script starts");
	
	CursorBrightnessFactor = 1.;
	HideBlockHelpers = False;
	
	LayersDefaultManialinkVersion = 3;
	
	declare CursorAction for ManialinkPage = 0;
	declare CursorX for ManialinkPage = 0;
	declare CursorY for ManialinkPage = 0;
	declare CursorZ for ManialinkPage = 0;
	declare CursorDirectionText for ManialinkPage = "";
	
	ManialinkText = CreateManialink();

	while(True) {
		yield;
		// update ManialinkPage variables
		CursorX = CursorCoord[0];
		CursorY = CursorCoord[1];
		CursorZ = CursorCoord[2];
		switch(CursorDir) {
			case ::CardinalDirections::North: 	CursorDirectionText = "North";
			case ::CardinalDirections::East: 	CursorDirectionText = "East";
			case ::CardinalDirections::South: 	CursorDirectionText = "South";
			case ::CardinalDirections::West: 	CursorDirectionText = "West";
			default: 							CursorDirectionText = "DirError";
		}
	}
}