#Include "Libs/Nadeo/Common/Core/MenuLayers.Script.txt"												as Layers
#Include "Libs/Nadeo/Common/Core/Pages.Script.txt"													as Pages
#Include "Libs/Nadeo/Common/Core/Logger.Script.txt"													as Logger
#Include "TextLib"																				as TextLib

// APIs
#Include "Libs/Nadeo/MP4/Common/Components/ReplayBrowser/ReplayBrowserAPI.Script.txt"				as ReplayBrowserAPI
#Include "Libs/Nadeo/MP4/Common/Components/MediaBrowser/MediaBrowserController.Script.txt"			as MediaBrowserController

//Layers
#Include "Libs/Nadeo/MP4/Common/Components/ReplayBrowser/LBReplayBrowser.Script.txt"				as LBReplayBrowser

#Const C_BrowserName	"ReplayBrowser"
#Const C_PageName		"BrowseReplays"
//L16N |Noun, plural| The files playing back a sequence of gameplay with customizable camera
#Const C_MediaTitle		_("Replays")

#Const C_Context1		"MakerCampaign"
#Const C_Context2		"MatchSettingsEditor"
#Const C_Context3		"LocalPlaySingleMap"
#Const C_Context4		"EditorsClassicMapEditor"
#Const C_Context5		"GhostEditor"
#Const C_Context6		"CustomEditor"

declare Text G_FolderPath;

Void Load() {
	ReplayBrowserAPI::Load();
	MediaBrowserController::Load(LBReplayBrowser::Build(), C_PageName, C_BrowserName, C_MediaTitle);
}

Void OpenCurrentFolder()
{
	ReplayBrowserAPI::FetchData(G_FolderPath);
}

Void OpenInExplorer()
{
	if (!OpenFileInExplorer(ReplayBrowserAPI::FullPath(G_FolderPath))) {
		Logger::Warn(C_BrowserName ^ " : OpenFileInExplorer did not function");
	}
}

Boolean IsRunning()
{
	return MediaBrowserController::IsRunning(C_BrowserName);
}

Void Show(Text _EventNamePrefix, Text[] _ButtonNames, Text _MediaTitle, Text[] _SecondaryButtonNames, Text _AddToListButtonName)
{
	G_FolderPath = "";
	ReplayBrowserAPI::FetchData("");
	MediaBrowserController::Show(C_BrowserName, C_PageName, _EventNamePrefix, _ButtonNames, _MediaTitle, _SecondaryButtonNames, _AddToListButtonName);
}

Void Show(Text _EventNamePrefix, Text[] _ButtonNames, Text _MediaTitle, Text[] _SecondaryButtonNames)
{
	G_FolderPath = "";
	ReplayBrowserAPI::FetchData("");
	MediaBrowserController::Show(C_BrowserName, C_PageName, _EventNamePrefix, _ButtonNames, _MediaTitle, _SecondaryButtonNames);
}

Void Show(Text _EventNamePrefix, Text[] _ButtonNames, Text _MediaTitle)
{
	Show(_EventNamePrefix, _ButtonNames, _MediaTitle, Text[]);
}

Void Hide()
{
	MediaBrowserController::Hide(C_BrowserName);
}

Void ForceHide()
{
	MediaBrowserController::ForceHide(C_BrowserName);
}

Boolean HasPathsList()
{
	return MediaBrowserController::HasPathsList(C_BrowserName);
}

Text[] GetPathsList()
{
	return MediaBrowserController::GetPathsList(C_BrowserName);
}

Text[][] LineDataFromContext(Text _Context, Text[Text] _Metadata)
{
	switch(_Context) {
		case "MakerCampaign" : {
			return		Text[][];
		}
		case "MatchSettingsEditor" : {
			
		}
		case "LocalPlaySingleMap" : {
			
		}
		case "EditorsClassicMapEditor" : {
			
		}
		case "GhostEditor" : {
			
		}
		case "CustomEditor" : {
			
		}
	}
	return Text[][];
}

Text[][][] GetLineData(Text _Context)
{
	declare Text[][][] Result;
	declare Text[Text][] MainData = ReplayBrowserAPI::GetMainData();
	foreach (Metadata in MainData) {
		if (Metadata.existskey("CardType") && Metadata["CardType"] == "Folder") {
			Result.add(Text[][]);
		} else {
			Result.add(LineDataFromContext(_Context, Metadata));
		}
	}
	return Result;
}

Text[][][] GetLineData()
{
	return GetLineData(C_Context1);
}

Void Loop() {
	if (ReplayBrowserAPI::HasFetched()) {
		ReplayBrowserAPI::UpdateMainData();
		MediaBrowserController::UpdateData(C_BrowserName, ReplayBrowserAPI::FullPath(G_FolderPath), ReplayBrowserAPI::GetMainData(), GetLineData(), ReplayBrowserAPI::GetCurrentPageIndex(), ReplayBrowserAPI::GetMaxPages());
	}
	if (MediaBrowserController::IsRunning(C_BrowserName)) {
		MediaBrowserController::Loop(C_BrowserName);
	}
	if (MediaBrowserController::IsRequestingFolderOpening(C_BrowserName)) {
		G_FolderPath = MediaBrowserController::FolderOpeningPath(C_BrowserName);
		OpenCurrentFolder();
	}
	if (MediaBrowserController::IsRequestingFolderBack(C_BrowserName)) {
		G_FolderPath = ReplayBrowserAPI::FolderParentPath();
		OpenCurrentFolder();
	}
	if (MediaBrowserController::IsRequestingRefreshFolder(C_BrowserName)) {
		ReplayBrowserAPI::Refresh();
		OpenCurrentFolder();
	}
	if (MediaBrowserController::IsRequestingOpenInExplorer(C_BrowserName)) {
		OpenInExplorer();
	}
	if (MediaBrowserController::IsRequestingSwitchFlatten(C_BrowserName)) {
		ReplayBrowserAPI::SwitchFlatten();
		OpenCurrentFolder();
	}
	if (MediaBrowserController::IsRequestingSortByDateName(C_BrowserName)) {
		ReplayBrowserAPI::SwitchSortCriterion();
		OpenCurrentFolder();
	}
	if (MediaBrowserController::IsRequestingSortAscDesc(C_BrowserName)) {
		ReplayBrowserAPI::SwitchSortOrder();
		OpenCurrentFolder();
	}
	if (MediaBrowserController::IsRequestingInputPageUp(C_BrowserName)) {
		ReplayBrowserAPI::InputPageUp();
		MediaBrowserController::UpdateData(C_BrowserName, ReplayBrowserAPI::FullPath(G_FolderPath), ReplayBrowserAPI::GetMainData(), GetLineData(), ReplayBrowserAPI::GetCurrentPageIndex(), ReplayBrowserAPI::GetMaxPages());
	}
	if (MediaBrowserController::IsRequestingInputPageUpTen(C_BrowserName)) {
		ReplayBrowserAPI::InputPageUpTen();
		MediaBrowserController::UpdateData(C_BrowserName, ReplayBrowserAPI::FullPath(G_FolderPath), ReplayBrowserAPI::GetMainData(), GetLineData(), ReplayBrowserAPI::GetCurrentPageIndex(), ReplayBrowserAPI::GetMaxPages());
	}
	if (MediaBrowserController::IsRequestingInputPageDown(C_BrowserName)) {
		ReplayBrowserAPI::InputPageDown();
		MediaBrowserController::UpdateData(C_BrowserName, ReplayBrowserAPI::FullPath(G_FolderPath), ReplayBrowserAPI::GetMainData(), GetLineData(), ReplayBrowserAPI::GetCurrentPageIndex(), ReplayBrowserAPI::GetMaxPages());
	}
	if (MediaBrowserController::IsRequestingInputPageDownTen(C_BrowserName)) {
		ReplayBrowserAPI::InputPageDownTen();
		MediaBrowserController::UpdateData(C_BrowserName, ReplayBrowserAPI::FullPath(G_FolderPath), ReplayBrowserAPI::GetMainData(), GetLineData(), ReplayBrowserAPI::GetCurrentPageIndex(), ReplayBrowserAPI::GetMaxPages());
	}
	if (MediaBrowserController::IsRequestingInputFirstPage(C_BrowserName)) {
		ReplayBrowserAPI::InputFirstPage();
		MediaBrowserController::UpdateData(C_BrowserName, ReplayBrowserAPI::FullPath(G_FolderPath), ReplayBrowserAPI::GetMainData(), GetLineData(), ReplayBrowserAPI::GetCurrentPageIndex(), ReplayBrowserAPI::GetMaxPages());
	}
	if (MediaBrowserController::IsRequestingInputLastPage(C_BrowserName)) {
		ReplayBrowserAPI::InputLastPage();
		MediaBrowserController::UpdateData(C_BrowserName, ReplayBrowserAPI::FullPath(G_FolderPath), ReplayBrowserAPI::GetMainData(), GetLineData(), ReplayBrowserAPI::GetCurrentPageIndex(), ReplayBrowserAPI::GetMaxPages());
	}
	if (MediaBrowserController::IsRequestingSteamWorkshop(C_BrowserName)) {
		TitleControl.PublishFile(ReplayBrowserAPI::FullPath(MediaBrowserController::RequestingSteamWorkshopMediaName(C_BrowserName)));
	}
	if (MediaBrowserController::IsRequestingSelectAll(C_BrowserName)) {
		MediaBrowserController::SelectAll(C_BrowserName, ReplayBrowserAPI::FilePathsInFolder());
	}
}